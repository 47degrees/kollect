apply from: 'detekt.gradle'

buildscript {
    ext {
        gradleVersionsPluginVersion = '0.17.0'
        javaVersion = JavaVersion.VERSION_1_7
        kotlinVersion = '1.3.11'
        kotlinxCoroutinesVersion = '1.1.0'
        kotlinxCollectionsImmutableVersion = '0.1'
        arrowVersion = '0.9.0-SNAPSHOT'
    }

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'io.arrow-kt:ank-gradle-plugin:0.6.0'
        classpath 'org.ajoberstar:gradle-git-publish:0.3.2'
    }
}

subprojects { project ->

    group = GROUP
    version = VERSION_NAME

    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local/" }
    }

    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    archivesBaseName = POM_ARTIFACT_ID

    jacoco {
        toolVersion '0.7.8'
    }

    task codeCoverageReport(type: JacocoReport) {
        reports {
            xml.enabled true
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
            html.enabled true
            csv.enabled false
        }

        classDirectories = fileTree(
                dir: 'build/classes/main/kollect',
        )

        sourceDirectories = files('src/main/kotlin')
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    }

    apply plugin: 'com.jfrog.bintray'

    bintray {
        publish = true
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        configurations = ['archives']
        pkg {
            repo = 'maven'
            name = project.name
            userOrg = POM_DEVELOPER_ID
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/47deg/kollect.git'
        }
    }

    compileKotlin.kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }

    build.dependsOn ':detekt'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

apply plugin: 'com.github.ben-manes.versions'
